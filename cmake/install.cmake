# Since this script will be run "externally", without access to the CMake cache/variables,
# we need to store them locally at configure-time.
file(TO_CMAKE_PATH "${TARGET_LINK_DIRECTORIES}" TARGET_LINK_DIRECTORIES)
file(TO_CMAKE_PATH "${CMAKE_LIBRARY_PATH}" CMAKE_LIBRARY_PATH)
file(TO_CMAKE_PATH "${CMAKE_SYSTEM_LIBRARY_PATH}" CMAKE_SYSTEM_LIBRARY_PATH)
file(TO_CMAKE_PATH "${REDUX_INSTALL_DIR}" REDUX_INSTALL_DIR)
file(TO_CMAKE_PATH "${CMAKE_PREFIX_PATH}" CMAKE_PREFIX_PATH)
file(TO_CMAKE_PATH "${CMAKE_SYSTEM_PREFIX_PATH}" CMAKE_SYSTEM_PREFIX_PATH)
file(TO_CMAKE_PATH "${CMAKE_SYSTEM_FRAMEWORK_PATH}" CMAKE_SYSTEM_FRAMEWORK_PATH)
file(TO_CMAKE_PATH "${TARGET_LOCATION}" TARGET_LOCATION)
if("$${}{CMAKE_INSTALL_CONFIG_NAME}" MATCHES "^([Dd][Ee][Bb][Uu][Gg])$")
    file(TO_CMAKE_PATH "${TARGET_DEBUG_LOCATION}" TARGET_LOCATION)
endif()
set(CMAKE_FIND_LIBRARY_PREFIXES "${CMAKE_FIND_LIBRARY_PREFIXES}")
set(CMAKE_FIND_LIBRARY_SUFFIXES ";${CMAKE_FIND_LIBRARY_SUFFIXES}")
set(CMAKE_LIBRARY_ARCHITECTURE "${CMAKE_LIBRARY_ARCHITECTURE}")
set(CMAKE_CL_64 "${CMAKE_CL_64}")
set(SKIP_SYSTEM_LIBS "0") # search system folders on linux, but not windows.

if(NOT EXISTS "${TARGET_LOCATION}")
    message(STATUS "File \"${TARGET_LOCATION}\" does not exist, skipping dependency installation.")
    return()
endif()

# append bin paths to locate .dll files on windows.
if(WIN32)
    set(SKIP_SYSTEM_LIBS "1")
    file(TO_CMAKE_PATH "$${}ENV{windir}" WINPATH)
    if(CMAKE_CL_64)
        set(DLL_PATHS "$${}{WINPATH}" "$${}{WINPATH}/SysWOW64" "$${}{WINPATH}/System32")
    else()
        set(DLL_PATHS "$${}{WINPATH}" "$${}{WINPATH}/System32" "$${}{WINPATH}/SysWOW64")
    endif()
    set(CMAKE_FIND_LIBRARY_SUFFIXES ".dll" $${}{CMAKE_FIND_LIBRARY_SUFFIXES})
    foreach(ldir $${}{TARGET_LINK_DIRECTORIES})
        string(REPLACE "/lib/" "/bin/" tmp_path "$${}{ldir}/")
        string(REPLACE "/lib64/" "/bin64/" bin_path "$${}{tmp_path}")
        if (IS_DIRECTORY "$${}{bin_path}")
            list(APPEND DLL_PATHS "$${}{bin_path}")
        endif()
    endforeach()
    list(APPEND CMAKE_LIBRARY_PATH $${}{DLL_PATHS} $${}{REDUX_INSTALL_DIR}/bin)
    list(REMOVE_DUPLICATES CMAKE_LIBRARY_PATH)
else()
    set(ENV{windir} "------") # just to avoid an empty variable for the matching below.
endif()

set(TARGET_LINK_DIRECTORIES $${}{CMAKE_LIBRARY_PATH} $${}{TARGET_LINK_DIRECTORIES} )
list(REMOVE_DUPLICATES TARGET_LINK_DIRECTORIES)

if(EXISTS "$${}{TARGET_LOCATION}")
    include(GetPrerequisites)
    get_prerequisites("$${}{TARGET_LOCATION}" TARGET_DEPS "$${}{SKIP_SYSTEM_LIBS}" 1 "" "$${}{TARGET_LINK_DIRECTORIES}")
    foreach(dep $${}{TARGET_DEPS})
        unset(libpath)
        unset(dep_realpath)
        unset(libpath CACHE)
        unset(dep_realpath CACHE)
        get_filename_component(dep_name "$${}{dep}" NAME)
        # This installation script is only aimed at thirdparty/system libraries, so we assume
        # they are always "up to date" and skip them if they already exist (less spam in output console)
        if(NOT EXISTS "$${}{REDUX_INSTALL_DIR}/bin/$${}{dep_name}")
            get_filename_component(dep_name_we "$${}{dep}" NAME_WE)
            get_filename_component(dep_realpath "$${}{dep}" REALPATH)
            get_filename_component(dep_rname "$${}{dep_realpath}" NAME)
            get_filename_component(dep_rpath "$${}{dep_realpath}" PATH)
            
            find_library(libpath NAMES "$${}{dep_rname}" "$${}{dep}" "$${}{dep_name}"
                                 PATHS "$${}{TARGET_LINK_DIRECTORIES};${CMAKE_SYSTEM_LIBRARY_PATH};$${}{dep_rpath}"
            )
            # Don't copy DLLs found in the windows system folders.
            if(NOT "$${}{libpath}" MATCHES "$${}{WINPATH}")
                if(EXISTS "$${}{libpath}")
                    get_filename_component(dep_realpath "$${}{libpath}" REALPATH)
                    file(INSTALL $${}{dep_realpath} DESTINATION "$${}{REDUX_INSTALL_DIR}/bin")
                    # can we avoid coying to build-dir by adding "$${}{REDUX_INSTALL_DIR}/bin" to environment for test targets ??
                    file(INSTALL $${}{dep_realpath} DESTINATION "${${MY_PROJECT}_BINARY_DIR}")
                    get_filename_component(inst_name "$${}{dep_realpath}" NAME)
                    if(NOT "$${}{dep_name}" STREQUAL "$${}{inst_name}")
                        file(RENAME "$${}{REDUX_INSTALL_DIR}/bin/$${}{inst_name}" "$${}{REDUX_INSTALL_DIR}/bin/$${}{dep_name}")
                    endif()
                elseif(EXISTS "$${}{dep_realpath}")
                    file(INSTALL $${}{dep_realpath} DESTINATION "$${}{REDUX_INSTALL_DIR}/bin")
                    file(INSTALL $${}{dep_realpath} DESTINATION "${${MY_PROJECT}_BINARY_DIR}")
                    get_filename_component(inst_name "$${}{dep_realpath}" NAME)
                    if(NOT "$${}{dep_name}" STREQUAL "$${}{inst_name}")
                        file(RENAME "$${}{REDUX_INSTALL_DIR}/bin/$${}{inst_name}" "$${}{REDUX_INSTALL_DIR}/bin/$${}{dep_name}")
                    endif()
                endif()
            endif()
        endif()
    endforeach()
else()
    message(WARNING "Target does not exist: \"$${}{TARGET_LOCATION}\"")
endif()
